plugins {
    id "com.bmuschko.nexus" version "2.3.1"
    id "com.github.kt3k.coveralls" version "2.6.3"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

archivesBaseName = 'android-sdk'
group = "io.xenn"
version = "2.6"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "2.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
}
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['test']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    // The lines below make sure we can report against Kotlin and exclude some Android Stuff
    def fileFilter = [
            '**/*Activity.class',
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
            '**/*Test*.*', 'android/**/*.*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    testImplementation "androidx.test:core:1.2.0"
    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation "androidx.test:runner:1.2.0"
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.json:json:20180813"
    testImplementation group: 'org.robolectric', name: 'robolectric', version: '4.3.1'
    testImplementation group: 'org.mock-server', name: 'mockserver-client-java', version: '5.10.0'
    testImplementation group: 'org.mock-server', name: 'mockserver-netty', version: '5.10.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
}

modifyPom {
    project {
        name 'android-sdk'
        description 'Xenn.io Android SDK'
        url 'https://xenn.io'
        inceptionYear '2019'
        developers {
            developer {
                id 'devxenn'
                name 'Xenn Developer'
                email 'developer@xenn.io'
            }
        }
        scm {
            url 'https://xenn.io'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

    }
}

publishing {
    publications {
        androidSdk(MavenPublication) {
            groupId "io.xenn"
            artifactId "android-sdk"
            version "2.6"
            artifact "$buildDir/outputs/aar/android-sdk-release.aar"
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}
